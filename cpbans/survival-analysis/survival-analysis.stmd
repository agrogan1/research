% Survival Analysis of Corporal Punishment Bans 
% Andy Grogan-Kaylor 
% `s c(current_date)` `s c(current_time)`

---
geometry: margin=.5 in
---

<style>h1 {color: #00274C;} h2 {color: #2F65A7;} blockquote {border-left: 5px solid #ffcb05; margin: 1.5em 10px; padding: 0.5em 10px;} code {color: red;}</style>

# Introduction

Corporal punishment is associated with a number of deleterious outcomes for children, including increases in behavior problems and mental health problems.

A number of countries have instituted country wide bans upon the use of corporal punishment with children.

Below, we employ a variety of empirical strategies to explore the institution of these bans.

# Get The Data

    use "../CPBans_w_AllCountries.dta", clear // data with ALL Countries; not just bans
	
> NB It is important to have data with the *correct risk set* which includes *all countries*, not just *countries that eventually ban corporal punishment*.	

# `stset` The Data

	generate current_year = year(today()) // variable with current year

	replace year_of_prohibition = current_year if year_of_prohibition == . // replace missing w/ current year
		
	generate f = type == "CP Ban" // "failure" variable

    stset year_of_prohibition, failure(f = 1) // stset the data with time and failure variables

# Graphs

## Survival Function

    sts graph, scheme(michigan) tmin(1970) // Kaplan-Meier Survivor Function
	
	graph export mysurvival.png, width(500) replace

![Kaplan-Meier Survivor Function](mysurvival.png)
 
## Failure Function

    sts graph, failure scheme(michigan) tmin(1970) // Kaplan-Meier Failure Function
	
	graph export myfailure.png, width(500) replace

![Kaplan-Meier Failure Function](myfailure.png)
	
# Survival Analysis

> Unlike other regression commands in Stata, survival analysis commands seem to require covariates.

## Data Wrangling

    encode continent, generate(continent_NUMERIC) // numeric version of continent
	
> Since Europe is where these bans started, we will use Europe (category 4) as the reference category.

## Parametric Survival Models

### Weibull

    streg ib4.continent_NUMERIC, distribution(weibull) // Weibull distribution
	
### Exponential

    streg ib4.continent_NUMERIC, distribution(exponential) // Exponential distribution

## Cox Proportional Hazards Model

    stcox ib4.continent_NUMERIC // Cox Proportional Hazards Model
		
### Survival Curves

	stcurve, survival scheme(michigan) // survival curve

	graph export survival1A.png, width(500) replace
	
![Survival Curve](survival1A.png)

	stcurve, failure scheme(michigan) // failure curve

	graph export survival1B.png, width(500) replace
	
![Failure Curve](survival1B.png)
	
	stcurve, failure at(continent_NUMERIC= (1 2 3 4 5 6)) ///
	legend(order(1 "Africa" 2 "Americas" 3 "Asia" ///
	4 "Europe" 5 "NA" 6 "Oceania")) ///
	scheme(michigan) // survival curve by continent
	
	graph export survival2.png, width(500) replace

![Failure Curve By Continent](survival2.png)

### Proportional Hazards Assumption
	
	estat phtest // formal test of PH assumption
	
	stphplot, by(continent_NUMERIC) scheme(michigan) // graphical test of PH assumption
	
    graph export ph.png, width(500) replace

![Graphical Test of Proportional Hazards Assumption](ph.png)

# Life Table

    ltable year_of_prohibition f, graph failure scheme(michigan) // lifetable

	graph export myltable.png, width(500) replace
	
![Graph Of Life Table](myltable.png)

# Discrete Time Survival Analysis

Discrete time survival analysis is placed at the end because it requires us to restructure the data into a long format, where every row is a *country-year*.

## Restructuring the Data

We first need to generate a variable for the years during which a country was "at risk" of enacting a ban. Countries that have never enacted a ban are at risk up until the current year. Countries that enacted a ban leave the risk set once they have enacted a ban, and are thus at risk for a shorter time period.

    generate years_at_risk =  year_of_prohibition - 1978 + 1 // years "at risk" of enacting a ban
	
We *expand* the data to generate one row of data for every year that each country is at risk.
    
	expand years_at_risk // "expand" the data; 1 row for every year at risk
	
We create a year variable.
	
	bysort country_code: generate year = _n + 1977 // generate a year variable for each row
	
Lastly, we generate an indicator of the event, a `0/1`' variable, which takes the value 1 for rows in which a ban was enacted, and 0 otherwise.
	
	generate event = type == "CP Ban" & year_of_prohibition == year // generate an event indicator

We list out a sample of the data to make sure that the data conform to our expectations. We focus on Norway, a country that *has* enacted a ban, and Great Britain, a country that *has not* enacted a ban.

	list country_code year_of_prohibition event continent years_at_risk year ///
	if country_code == "NOR" | country_code == "GBR" , ab(20) // list out a sample of the data
	
# Analysis

Lastly, we analyze the data using a straightforward logistic regression model. While there is some discussion on this point, we choose not to cluster the standard errors on country, because of the argument from Singer and Willett (2003) that the rows of data are *conditionally* independent.

We ask for *odds ratios* so that our results are roughly comparable to those from the continuous time survival models.

    logit event ib4.continent_NUMERIC, cluster(country_code) or
	
# References

Allison, P. D. (1984). *Event History Analysis: Regression for Longitudinal Event Data*. SAGE Publications. Retrieved from https://books.google.com/books?id=XkACeUEiEsUC
	
Singer, J. D., & Willett, J. B. (2003). *Applied longitudinal data analysis : modeling change and event occurrence*. Oxford ; New York : Oxford University Press.	

StataCorp. 2021. *Stata 17 Survival Analysis Reference Manual*. College Station, TX: Stata Press
	
	